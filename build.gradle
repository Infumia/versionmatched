plugins {
  id 'java'
  id 'java-library'
  id 'checkstyle'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'tr.com.infumia'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava {
  options.encoding = 'UTF-8'
}

javadoc {
  options.tags = ['todo']
  options.addBooleanOption('html5', true)
}

jar {
  archiveBaseName.set(project.name)
  archiveBaseName.convention(project.name)
  archiveClassifier.set(null)
  archiveClassifier.convention(null)
  archiveVersion.set(null)
  archiveVersion.convention(null)
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveBaseName.set(project.name)
  archiveBaseName.convention(project.name)
  archiveClassifier.set("sources")
  archiveClassifier.convention("sources")
  archiveVersion.set(null)
  archiveVersion.convention(null)
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveBaseName.set(project.name)
  archiveBaseName.convention(project.name)
  archiveClassifier.set("javadoc")
  archiveClassifier.convention("javadoc")
  archiveVersion.set(null)
  archiveVersion.convention(null)
  from javadoc.destinationDir
}

checkstyle {
  configFile = file('checkstyle.xml')
  checkstyleTest.enabled = false
}

test {
  useJUnitPlatform()
}

repositories {
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
  mavenLocal()
}

dependencies {
  // Annotations
  compileOnlyApi "org.jetbrains:annotations:${annotations}"
  annotationProcessor "org.jetbrains:annotations:${annotations}"
  compileOnlyApi "org.projectlombok:lombok:${lombok}"
  annotationProcessor "org.projectlombok:lombok:${lombok}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombok}"
  testAnnotationProcessor "org.jetbrains:annotations:${annotations}"
  // Annotations

  implementation 'tr.com.infumia:reflection:0.1.4-SNAPSHOT'
  implementation 'tr.com.infumia:bukkitversion:0.1.0-SNAPSHOT'
  compileOnlyApi 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'

  testImplementation "org.junit.jupiter:junit-jupiter:${junitjupiter}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      from components.java
      artifact tasks.sourcesJar
      artifact tasks.javadocJar
      pom {
        name = 'VersionMatched'
        description = 'A Minecraft library to match version specific classes with the running server version.'
        url = 'https://infumia.com.tr/'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://mit-license.org/license.txt'
          }
        }
        developers {
          developer {
            id = 'portlek'
            name = 'Hasan Demirta≈ü'
            email = 'utsukushihito@outlook.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/infumia/versionmatched.git'
          developerConnection = 'scm:git:ssh://github.com/infumia/versionmatched.git'
          url = 'https://github.com/infumia/versionmatched'
        }
      }
    }
  }
}

tasks.withType(Sign).configureEach {
  onlyIf { project.property("dev") == "false" }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
